plugins {
    id 'scala'
    id 'java'
}

group "com.example"
version "1.0-SNAPSHOT"

repositories {
    jcenter()
}

configurations {
    exclusion
}

dependencies {
    implementation platform("ai.djl:bom:0.8.0")
    implementation "org.apache.spark:spark-sql_2.12:3.0.1"
    implementation "org.apache.spark:spark-mllib_2.12:3.0.1"
    implementation "org.apache.hadoop:hadoop-hdfs:2.7.4"
    // exclude system provided dependencies to avoid version mismatch
    exclusion "org.apache.spark:spark-sql_2.12:3.0.1"
    exclusion "org.apache.spark:spark-mllib_2.12:3.0.1"
    exclusion "org.apache.hadoop:hadoop-hdfs:2.7.4"
    implementation "ai.djl:api"


    runtimeOnly "ai.djl.pytorch:pytorch-model-zoo"
    runtimeOnly "ai.djl.pytorch:pytorch-native-cu101::linux-x86_64"
    runtimeOnly "org.slf4j:slf4j-simple:1.7.30"
}

configure(this) {
    defaultTasks "jar"
}

compileScala {
    scalaCompileOptions.setAdditionalParameters(["-target:jvm-1.8"])
}

jar {
    from {
        (configurations.runtimeClasspath - configurations.exclusion).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes["Main-Class"] = "com.examples.ImageClassificationExample"
    }
}
